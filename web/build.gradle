plugins {
    id 'java'
}

apply plugin: 'war'

group 'by.seledtsovaos'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}


configurations {
    hsqldb
}

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.7.2'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.7.2'
    hsqldb group: 'org.hsqldb', name: 'hsqldb', version: '2.6.0'
    implementation 'org.mapstruct:mapstruct:1.5.2.Final'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.10'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'
    implementation group: 'org.hsqldb', name: 'hsqldb', version: '2.6.0'

}
apply plugin: 'checkstyle'

checkstyle {
    toolVersion = '7.1.1'
    configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    ignoreFailures = false
}
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

/**
 * Starts the database.
 */
task startDatabase {
    group "Database"
    description "Starts the database."
    doLast {
        javaexec {
            main = "org.hsqldb.server.Server";
            args = ['--database.0', 'mydb', '--dbname.0', 'crud']
            classpath = configurations.hsqldb;
        }
    }
}
/**
 * Runs scripts to create tables in the database.
 */
task createSql {
    group "Database"
    description "Creates table in database."
    doLast {
        def driverName = 'org.hsqldb.jdbc.JDBCDriver';

        ant.sql(classpath: configurations.hsqldb.asPath,
            driver: driverName,
            url: 'jdbc:hsqldb:hsql://localhost/crud',
            userid: 'SA',
            password: '') {
            fileset(file: 'src/main/resources/sql/createTable.sql')
        }
    }
}
/**
 * Runs scripts to drop tables from database.
 */
task dropSql {
    group "Database"
    description "Drops table from database."
    doLast {
        def driverName = 'org.hsqldb.jdbc.JDBCDriver';
        ant.sql(classpath: configurations.hsqldb.asPath,
            driver: driverName,
            url: 'jdbc:hsqldb:hsql://localhost/crud',
            userid: 'SA',
            password: '') {
            fileset(file: 'src/main/resources/sql/dropTable.sql')
        }
    }
}
